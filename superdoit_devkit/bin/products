#!/usr/bin/env superdoit_solo
instvars
	installedProducts
%
options
{
  SuperDoitCommandLineOption long: 'help' short: 'h'.
	SuperDoitCommandLineOption long: 'debug'.
}
%
usage
-----
USAGE $basename [--help | -h]

DESCRIPTION
  Provide information on the downloaded GemStone versions.

OPTIONS
  -h, --help 		display usage message

EXAMPLES
  $basename --help
  $basename
-----
%
method
getAndVerifyOptions
	self getOpts: self optionSpecs.
	self help ifTrue: [ ^ self usage ].
%
method
produceProductsReport
	| stream |
	stream := self stderr.
	self installedProductsReportOn: stream
%
method
installedProductsReportOn: stream
	stream
		nextPutAll: 'Installed Products:';
		cr.
	self installedProducts keys asSortedCollection
		do: [ :gsVers |
			stream
				tab;
				nextPutAll: gsVers;
				cr ]
%
method
installedProducts
	installedProducts
		ifNil: [
			installedProducts := Dictionary new.
			self gs_productsDirectory directories
				do: [ :productDir |
					| dirName |
					dirName := productDir basename.
					(dirName indexOfSubCollection: 'GemStone64Bit') == 1
						ifTrue: [
							| productVersion dashIndex |
							dashIndex := dirName indexOf: $-.
							productVersion := dirName copyFrom: 'GemStone64Bit' size + 1 to: dashIndex - 1.
							installedProducts at: productVersion put: productDir ]
						ifFalse: [
							(dirName indexOfSubCollection: 'GemBuilderC') == 1
								ifTrue: [
									| productVersion dashIndex |
									dashIndex := dirName indexOf: $-.
									productVersion := dirName copyFrom: 'GemBuilderC' size + 1 to: dashIndex - 1.
									installedProducts at: productVersion put: productDir ] ] ] ].
	^ installedProducts
%
method
gs_productsDirectory
	^  ((System gemEnvironmentVariable: 'GS_HOME'), '/shared/downloads/products') asFileReference
%
doit
	[
		self getAndVerifyOptions == self noResult
			ifTrue: [ ^ self noResult ].
		self produceProductsReport.
		^ self noResult
	] on: Error do: [:ex |
		self debug ifTrue: [ ex pass ].
		self exit: ex messageText withStatus: 1 "does not return" ].
%
